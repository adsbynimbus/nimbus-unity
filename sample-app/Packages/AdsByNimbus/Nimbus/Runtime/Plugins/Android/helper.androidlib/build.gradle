rootProject.allprojects {
    repositories {
        maven {
            url = uri("https://adsbynimbus-public.s3.amazonaws.com/android/sdks")
            credentials {
                username = "*"
            }
        }
    }
}

apply plugin: 'com.android.library'

int[] sdksAvailable() {
    def sdks = new ByteArrayOutputStream()
    exec {
        commandLine 'android', 'list'
        standardOutput = sdks
    }
    sdks = sdks.toString().split('\n')
            .findAll { it ==~ /\s*API level:.*/ }
            // extract the API levels
            .collect { (it =~ /\s*API level:\s*(\d+).*/)[0][1].toInteger() }
            // sort from highest to lowest
            .sort( { a, b -> b <=> a } )
    sdks
}

int highestSdkAvailable(int defaultSdk) {
    try {
        def highestSdk = sdksAvailable()[0]
        if (highestSdk != null) {
            println "Using highest found SDK " + highestSdk
            highestSdk
        } else {
            println "No installed SDKs found. Using default SDK $defaultSdk"
            defaultSdk
        }
    } catch (any) {
        println "Exception while determining highest SDK. Using default SDK $defaultSdk"
        defaultSdk
    }
}

android {
    compileSdkVersion highestSdkAvailable(28)

    defaultConfig {
        minSdkVersion 17
        consumerProguardFiles 'consumer-proguard-rules.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation "com.adsbynimbus.android:nimbus:1.10.0"
    implementation "com.adsbynimbus.android:extension-okhttp:1.10.0"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.4.31"
}
